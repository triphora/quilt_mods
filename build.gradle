import com.emmacypress.gradle.ConfigurationExtension
import com.emmacypress.gradle.Constants
import com.emmacypress.gradle.MixinsJsonGenerator
import com.emmacypress.gradle.QuiltModJsonGenerator

plugins {
	alias libs.plugins.quilt.loom apply false
	alias libs.plugins.minotaur apply false
	alias libs.plugins.machete apply false
	alias libs.plugins.outlet apply false
}

subprojects {
	apply plugin: 'java'
	apply plugin: 'org.quiltmc.loom'
	apply plugin: 'com.modrinth.minotaur'
	apply plugin: 'io.github.p03w.machete'
	apply plugin: 'io.github.dexman545.outlet'

	group = Constants.GROUP
	archivesBaseName = project.name

	repositories {
		exclusiveContent {
			forRepository {
				maven {
					name = 'Modrinth'
					url = 'https://api.modrinth.com/maven'
				}
			}
			filter {
				includeGroup 'maven.modrinth'
			}
		}
		exclusiveContent {
			forRepository {
				maven {
					name = 'TerraformersMC'
					url = 'https://maven.terraformersmc.com'
				}
			}
			filter {
				includeGroup 'com.terraformersmc'
			}
		}
		exclusiveContent {
			forRepository {
				maven {
					name = 'Auoeke'
					url = 'https://maven.auoeke.net'
				}
			}
			filter {
				includeGroup 'net.auoeke'
				includeGroup 'net.gudenau.lib'
			}
		}
	}

	loom {
		runtimeOnlyLog4j = project.name != 'clean_logs'
		runs {
			configureEach {
				vmArg '-Dmixin.debug.export=true'
			}
		}
	}

	tasks.named('prepareRemapJar') {
		dependsOn {
			tasks.named('optimizeOutputsOfJar')
		}
	}

	java.withSourcesJar()

	dependencies {
		minecraft libs.minecraft
		mappings(variantOf(libs.quilt.mappings) { classifier 'intermediary-v2' })
		modApi libs.quilt.loader

		modLocalRuntime libs.bundles.runtime
	}

	extensions.create('emmod', ConfigurationExtension.class, project)
	task generateResources {
		doLast {
			new MixinsJsonGenerator().create(project)
			new QuiltModJsonGenerator().create(project)
		}
		outputs.dir('build/generated/resources')
	}

	sourcesJar.dependsOn generateResources
	prepareRemapJar.dependsOn generateResources

	sourceSets {
		main {
			resources {
				srcDir generateResources
			}
		}
	}

	if (emmod.qsl || emmod.midnightlib) {
		dependencies {
			modApi libs.quilted.fabric.api
		}
	}

	if (emmod.midnightlib) {
		dependencies {
			modApi libs.midnightlib
			include libs.midnightlib
		}
	}

	int javaVersion = emmod.javaVersion.orNull ?: 17

	tasks.withType(JavaCompile).configureEach {
		it.options.encoding = 'UTF-8'
		it.options.release = javaVersion
		it.sourceCompatibility = javaVersion
	}

	jar.from project.file('LICENSE').exists() ? project.file('LICENSE') : rootProject.file('LICENSE')

	modrinth {
		debugMode = true
		projectId = emmod.modrinthSlug.orElse project.name
		gameVersions = [libs.versions.minecraft.get()]
		versionName = System.getenv().VERSION_NAME ?: versionNumber
		changelog = System.getenv().CHANGELOG ?: DEFAULT_CHANGELOG
		versionType = System.getenv().VERSION_TYPE ?: DEFAULT_VERSION_TYPE
		uploadFile = remapJar
		additionalFiles = [sourcesJar]
		syncBodyFrom = project.file('README.md').text + '\n' +
			(emmod.qsl || emmod.midnightlib ? rootProject.file('QSL.md').text : '') +
			'\n' + rootProject.file('COMMON.md').text
	}

	tasks.modrinth.doFirst {
		if (emmod.gameVersions) {
			modrinth.gameVersions = emmod.gameVersions
		}

		if (emmod.qsl || emmod.midnightlib) {
			modrinth {
				dependencies {
					required.project 'qsl'
				}
			}
		}

		if (emmod.midnightlib) {
			modrinth {
				dependencies {
					embedded.project 'midnightlib'
					optional.project 'modmenu'
				}
			}
		}
	}

	tasks.modrinth.dependsOn tasks.modrinthSyncBody
}
