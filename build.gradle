import dev.triphora.gradle.ConfigurationExtension
import dev.triphora.gradle.Constants
import dev.triphora.gradle.MixinsJsonGenerator
import dev.triphora.gradle.QuiltModJsonGenerator

plugins {
	alias libs.plugins.quilt.loom apply false
	alias libs.plugins.minotaur apply false
	alias libs.plugins.machete apply false
	alias libs.plugins.outlet apply false
}

subprojects {
	apply plugin: 'java'
	//apply plugin: 'maven-publish'
	apply plugin: 'org.quiltmc.loom'
	apply plugin: 'com.modrinth.minotaur'
	apply plugin: 'io.github.p03w.machete'
	apply plugin: 'io.github.dexman545.outlet'

	group = Constants.GROUP
	archivesBaseName = project.name

	[
		['Modrinth', 'api.modrinth.com/maven', ['maven.modrinth']],
	].forEach { var mavenInfo ->
		repositories {
			exclusiveContent {
				forRepository {
					maven {
						name = mavenInfo[0]
						url = 'https://' + mavenInfo[1]
					}
				}
				filter {
					mavenInfo[2].forEach { String group ->
						includeGroup group
					}
				}
			}
		}
	}

	loom {
		runtimeOnlyLog4j = project.name != 'clean_logs'
		runs {
			configureEach {
				vmArg '-Dmixin.debug.export=true'
			}
		}
	}

	java.withSourcesJar()

	dependencies {
		minecraft libs.minecraft
		mappings(variantOf(libs.quilt.mappings) { classifier 'intermediary-v2' })
		modApi libs.quilt.loader

		modLocalRuntime(libs.bundles.runtime) {
			transitive = false
		}
	}

	extensions.create('emmod', ConfigurationExtension.class, project)
	task generateResources {
		doLast {
			new MixinsJsonGenerator().create(project)
			new QuiltModJsonGenerator().create(project)
		}
		outputs.dir('build/generated/resources')
	}

	sourcesJar.dependsOn generateResources
	remapJar.dependsOn generateResources

	sourceSets {
		main {
			resources {
				srcDir generateResources
			}
		}
	}

	if (emmod.qsl || emmod.midnightlib) {
		dependencies {
			modApi libs.quilted.fabric.api
		}
	}

	if (emmod.midnightlib) {
		dependencies {
			modApi libs.midnightlib
			include libs.midnightlib
		}
	}

	int javaVersion = emmod.javaVersion.orNull ?: 17

	tasks.withType(JavaCompile).configureEach {
		it.options.encoding = 'UTF-8'
		it.options.release = javaVersion
		it.sourceCompatibility = javaVersion
	}

	jar.from project.file('LICENSE').exists() ? project.file('LICENSE') : rootProject.file('LICENSE')

	def projectBody = project.file('README.md').text
	def devinsBadges = 'https://cdn.jsdelivr.net/npm/@intergrav/devins-badges@3/assets/cozy'

	if (emmod.qsl || emmod.midnightlib) {
		projectBody += "\n[![Requires Quilt Standard Libraries]($devinsBadges/requires/quilt-standard-libraries_64h.png)]" +
			'(https://modrinth.com/mod/qsl)'
	}

	projectBody += "\n---\n[![Support us through GitHub Sponsors]($devinsBadges/donate/ghsponsors-plural_64h.png)]" +
		"($Constants.DONATE_LINK)\\\n[![Chat with us on Discord]($devinsBadges/social/discord-plural_64h.png)]" +
		"($Constants.DISCORD_LINK)"

	modrinth {
		projectId = emmod.modrinthSlug.orElse project.name
		gameVersions = [libs.versions.minecraft.get()]
		versionName = System.getenv().VERSION_NAME ?: versionNumber
		changelog = System.getenv().CHANGELOG ?: DEFAULT_CHANGELOG
		versionType = System.getenv().VERSION_TYPE ?: DEFAULT_VERSION_TYPE
		uploadFile = remapJar
		additionalFiles = [sourcesJar]
		syncBodyFrom = projectBody
	}

	tasks.modrinth.doFirst {
		if (emmod.gameVersions) {
			modrinth.gameVersions = emmod.gameVersions
		}

		if (emmod.qsl || emmod.midnightlib) {
			modrinth {
				dependencies {
					required.project 'qsl'
				}
			}
		}

		if (emmod.midnightlib) {
			modrinth {
				dependencies {
					embedded.project 'midnightlib'
					optional.project 'modmenu'
				}
			}
		}
	}

	tasks.modrinth.dependsOn tasks.modrinthSyncBody
	tasks.modrinth.dependsOn { tasks.named('optimizeOutputsOfRemapJar') }

	//publishing {
	//	publications {
	//		mavenJava(MavenPublication) {
	//			from components.java
	//		}
	//	}
	//	repositories {
	//		maven {
	//			url = 's3://maven'
	//			credentials(AwsCredentials) {
	//				accessKey = findProperty('dev.triphora.maven.access')
	//				secretKey = findProperty('dev.triphora.maven.secret')
	//			}
	//		}
	//	}
	//}
}
